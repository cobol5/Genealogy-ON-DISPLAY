  100 REM DISPLAY Program.
  110 REM Displays Genealogical Information
  120 REM Copyright (c) 1982 ... 1989 by: Melvin O. Duke.
  130 OPTION BASE 0
  140 DEFINT A-Z
  600 REM Titles
  610 TITLE$ = "Display Program"
  620 TITLE$ = TITLE$ + " ON DISPLAY"
  700 REM Terminate if not called from the Menu
  710 IF COPY2$ = "Melvin O. Duke" THEN 770
  720 COLOR 7,0 : KEY ON : CLS : LOCATE 15,1
  730 PRINT "Cannot run the"
  740 PRINT TITLE$
  750 PRINT "Program, unless selected from the MENU"
  760 END
  770 REM OK
  900 REM Dimension Statements
  940 DIM PERS(15), CH(MAX.PER)
 1000 REM Produce the first screen
 1010 KEY ON : CLS : KEY OFF
 1020 REM Find the title location
 1030 TITLE.POS = 40 - INT(LEN(TITLE$)/2)
 1040 REM Print the title
 1050 LOCATE 4,TITLE.POS : PRINT TITLE$
 1060 LOCATE 5,40-INT(LEN(VERSION$)/2) :  PRINT VERSION$;
 1070 REM Print the Copyright
 1080 LOCATE 20,40-INT(LEN(COPY1$)/2) :  PRINT COPY1$;
 1090 LOCATE 21,40-INT(LEN(COPY2$)/2) :  PRINT COPY2$;
 1100 GOTO 1700
 1110 REM subroutine to print a double box
 1120 COLOR P
 1130 FOR I = R1 + 1 TO R2 - 1
 1140  LOCATE I, C1 : PRINT CHR$(186);
 1150  LOCATE I, C2 : PRINT CHR$(186);
 1160 NEXT I
 1170  LOCATE R1, C1+1 : PRINT STRING$(C2-C1-1,205);
 1180  LOCATE R2, C1+1 : PRINT STRING$(C2-C1-1,205);
 1190  LOCATE R1, C1 : PRINT CHR$(201);
 1200  LOCATE R1, C2 : PRINT CHR$(187);
 1210  LOCATE R2, C1 : PRINT CHR$(200);
 1220  LOCATE R2, C2 : PRINT CHR$(188);
 1230 COLOR W
 1240 RETURN
 1700 REM Display the Copyright
 1710 '
 1720 LOCATE 25,1
 1730 PRINT DATADISK$;
 1740 K$ = INKEY$ : IF K$ = "" THEN 1740
 1750 REM DISPLAY Program Starts Here.
 1760 KEY ON : CLS
 1770 GOTO 3530
 2000 REM Routine to Draw a Chart of Ancestors
 2010 REM Draw the form on the display
 2020 R1= 1 : C1= 1 : R2=21 : C2=79: GOSUB 1110 'Double box
 2030 R1= 3 : C1= 1 : R2= 3 : C2=79: GOSUB 3210 'Horizontal Double
 2040 LOCATE 2,3 : PRINT "Ancestors of:"
 2050 LOCATE 4,68 : COLOR N : PRINT "Birthdate:" : COLOR W
 2060 LOCATE 12, 3 : I = 1 : GOSUB 2470
 2070 LOCATE  8,11 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2080 LOCATE  9,11 : PRINT CHR$(179);
 2090 LOCATE 10,11 : PRINT CHR$(179);
 2100 LOCATE 11,11 : PRINT CHR$(179);
 2110 LOCATE  8,13 : I = 2 : GOSUB 2510
 2120 LOCATE 16,11 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2130 LOCATE 13,11 : PRINT CHR$(179);
 2140 LOCATE 14,11 : PRINT CHR$(179);
 2150 LOCATE 15,11 : PRINT CHR$(179);
 2160 LOCATE 16,13 : I = 3 : GOSUB 2510
 2170 LOCATE  6,21 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2180 LOCATE  7,21 : PRINT CHR$(179);
 2190 LOCATE  6,23 : I = 4 : GOSUB 2540
 2200 LOCATE 10,21 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2210 LOCATE  9,21 : PRINT CHR$(179);
 2220 LOCATE 10,23 : I = 5 : GOSUB 2540
 2230 LOCATE 14,21 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2240 LOCATE 15,21 : PRINT CHR$(179);
 2250 LOCATE 14,23 : I = 6 : GOSUB 2540
 2260 LOCATE 18,21 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2270 LOCATE 17,21 : PRINT CHR$(179);
 2280 LOCATE 18,23 : I = 7 : GOSUB 2540
 2290 LOCATE  5,31 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2300 LOCATE  5,33 : I = 8 : GOSUB 2570
 2310 LOCATE  7,31 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2320 LOCATE  7,33 : I = 9 : GOSUB 2570
 2330 LOCATE  9,31 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2340 LOCATE  9,33 : I = 10 : GOSUB 2570
 2350 LOCATE 11,31 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2360 LOCATE 11,33 : I = 11 : GOSUB 2570
 2370 LOCATE 13,31 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2380 LOCATE 13,33 : I = 12 : GOSUB 2570
 2390 LOCATE 15,31 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2400 LOCATE 15,33 : I = 13 : GOSUB 2570
 2410 LOCATE 17,31 : COLOR B : PRINT CHR$(218)+CHR$(196);
 2420 LOCATE 17,33 : I = 14 : GOSUB 2570
 2430 LOCATE 19,31 : COLOR B : PRINT CHR$(192)+CHR$(196);
 2440 LOCATE 19,33 : I = 15 : GOSUB 2570
 2450 COLOR W,K
 2460 RETURN
 2470 REM Routine to print the lines
 2480 COLOR K,W : PRINT RIGHT$(STR$(I),2);
 2490 COLOR B,K : PRINT STRING$(62,95);
 2500 RETURN
 2510 COLOR K,W : PRINT RIGHT$(STR$(I),2);
 2520 COLOR B,K : PRINT STRING$(52,95);
 2530 RETURN
 2540 COLOR K,W : PRINT RIGHT$(STR$(I),2);
 2550 COLOR B,K : PRINT STRING$(42,95);
 2560 RETURN
 2570 COLOR K,W : PRINT RIGHT$(STR$(I),2);
 2580 COLOR B,K : PRINT STRING$(32,95); : COLOR W,K
 2590 RETURN
 2600 REM Draw the Personal Information Chart
 2610 KEY OFF
 2620 R1 = 1 : C1 = 1 : R2 = 21 : C2 = 79 : GOSUB 1110  'Double box
 2630 R1 = 3 : C1 = 1 : R2 = 3 : C2 = 79 : GOSUB 3210  'Horizontal double
 2640 LOCATE 2,3 : PRINT "Personal Information for:"
 2650 R1 = 3 : C1 = 40 : R2 = 21 : C2 = 40 : GOSUB 3350  'Vertical Double
 2660 LOCATE 4,3 : COLOR N : PRINT "Person:";
 2670 LOCATE 5,3 : COLOR O : PRINT "Record-no.:";
 2680 LOCATE 6,3 : PRINT "Surname:";
 2690 LOCATE 7,3 : PRINT "Given-names:";
 2700 LOCATE 8,3 : PRINT "Sex:";
 2710 R1 = 9 : C1 = 1 : R2 =11 : C2 = 40 : GOSUB 3210  'Horizontal Double
 2720 LOCATE 10,3 : COLOR N : PRINT "Male Parent:";
 2730 LOCATE 11,3 : COLOR O : PRINT "Record-no.:";
 2740 LOCATE 12,3 : PRINT "Surname:";
 2750 LOCATE 13,3 : PRINT "Given-names:";
 2760 LOCATE 14,3 : PRINT "Birth-date:";
 2770 R1 = 15 : C1 = 1 : R2 = 15 : C2 = 40 : GOSUB 3280  'Horizontal Single
 2780 LOCATE 16,3 : COLOR N : PRINT "Female Parent:";
 2790 LOCATE 17,3 : COLOR O : PRINT "Record-no.:";
 2800 LOCATE 18,3 : PRINT "Surname:";
 2810 LOCATE 19,3 : PRINT "Given-names:";
 2820 LOCATE 20,3 : PRINT "Birth-date:";
 2830 LOCATE 4,42 : COLOR N : PRINT "Person's Vital Statistics:";
 2840 LOCATE 6,42 : COLOR O : PRINT "Birth-date:";
 2850 LOCATE 7,42 : PRINT "Birth-city:";
 2860 LOCATE 8,42 : PRINT "Birth-county:";
 2870 LOCATE 9,42 : PRINT "Birth-state:";
 2880 LOCATE 11,42 : PRINT "Death-date:";
 2890 LOCATE 12,42 : PRINT "Death-city:";
 2900 LOCATE 13,42 : PRINT "Death-county:";
 2910 LOCATE 14,42 : PRINT "Death-state:";
 2920 LOCATE 16,42 : PRINT "Burial-date:";
 2930 LOCATE 17,42 : PRINT "Burial-city:";
 2940 LOCATE 18,42 : PRINT "Burial-county:";
 2950 LOCATE 19,42 : PRINT "Burial-state:"; : COLOR W,K
 2960 RETURN
 2970 REM Draw a Family Group
 2980 KEY OFF
 2990 R1 = 1 : C1 = 1 : R2 = 21 : C2 = 79 : GOSUB 1110  'Double box
 3000 R1 = 3 : C1 = 1 : R2 = 3 : C2 = 79 : GOSUB 3210  'Double Horizontal
 3010 LOCATE 2,3 : PRINT "Family Group";
 3020 LOCATE 2,64 : PRINT "Marriage:";
 3030 LOCATE 4,3 : COLOR O : PRINT "Father:";
 3040 LOCATE 4,56 : PRINT "Birthdate:";
 3050 LOCATE 5,3 : PRINT "Mother:";
 3060 LOCATE 5,56 : PRINT "Birthdate:";
 3070 R1 = 6 : C1 = 1 : R2 = 6 : C2 = 79 : GOSUB 3280  'Single Horizontal
 3080 LOCATE 7,3 : COLOR O : PRINT "Marriage Date:";
 3090 LOCATE 7,35 : PRINT "Location:";
 3100 R1 = 8 : C1 = 1 : R2 = 8 : C2 = 79 : GOSUB 3210  'Double Horizontal
 3110 R1 = 8 : C1 = 5 : R2 = 21 : C2 = 5 : GOSUB 3440  'Single Vertical
 3120 R1 = 8 : C1 = 7 : R2 = 21 : C2 = 7 : GOSUB 3440  'Single Vertical
 3130 R1 = 8 : C1 = 40 : R2 = 21 : C2 = 40 : GOSUB 3440  'Single Vertical
 3140 LOCATE 9,2 : COLOR N : PRINT "No.";
 3150 LOCATE 9,6 : PRINT "S";
 3160 LOCATE 9,8 : PRINT "Children:";
 3170 LOCATE 9,41 : PRINT "Birthdate:";
 3180 R1 = 8 : C1 = 52 : R2 = 21 : C2 = 52 : GOSUB 3440  'Single Vertical
 3190 LOCATE 9,53 : COLOR N : PRINT "Birth Location:"; : COLOR W
 3200 RETURN
 3210 REM Subroutine to draw a double horizontal line.  Attach to double.
 3220 COLOR P
 3230  LOCATE R1,C1+1 : PRINT STRING$(C2-C1-1,205)
 3240 LOCATE R1,C1 : PRINT CHR$(204);
 3250 LOCATE R1,C2 : PRINT CHR$(185);
 3260 COLOR W
 3270 RETURN
 3280 REM Subroutine to draw a single horizontal line.  Attach to double.
 3290 COLOR P
 3300  LOCATE R1,C1+1 : PRINT STRING$(C2-C1-1,196)
 3310 LOCATE R1,C1 : PRINT CHR$(199);
 3320 LOCATE R1,C2 : PRINT CHR$(182);
 3330 COLOR W
 3340 RETURN
 3350 REM Subroutine to draw a double vertical line.  Attach to double.
 3360 COLOR P
 3370 FOR I = R1 + 1 TO R2 - 1
 3380  LOCATE I,C1 : PRINT CHR$(186);
 3390 NEXT I
 3400 LOCATE R1,C1 : PRINT CHR$(203);
 3410 LOCATE R2,C1 : PRINT CHR$(202);
 3420 COLOR W
 3430 RETURN
 3440 REM Subroutine to draw a single vertical line.  Attach to double.
 3450 COLOR P
 3460 FOR I = R1 + 1 TO R2 - 1
 3470  LOCATE I,C1 : PRINT CHR$(179);
 3480 NEXT I
 3490 LOCATE R1,C1 : PRINT CHR$(209);
 3500 LOCATE R2,C1 : PRINT CHR$(207);
 3510 COLOR W
 3520 RETURN
 3530 REM Program begins here
 3540 REM By:  Melvin O. Duke.
 3550 REM Read the Parent/Child Index
 3560 KEY OFF
 3570 LOCATE 4,1 : PRINT "Open the Parent/Child Index"
 3580 OPEN CC.PCINDEX$ FOR INPUT AS #1
 3590 INPUT #1, PC.COUNT
 3600 IF PC.COUNT <> 0 THEN 3650
 3610 PRINT "Parent/Child Index has no Index Records"
 3620 PRINT "Press any key to return to the Menu"
 3630 A$ = INKEY$ : IF A$ = "" THEN 3630
 3640 GOTO 9530
 3650 DIM PA.ID(PC.COUNT), CH.ID(PC.COUNT)
 3660 FOR I = 1 TO PC.COUNT
 3670 LOCATE 5,1 : PRINT "Reading Index Record #:";I;
 3680  INPUT #1, PA.ID(I), CH.ID(I)
 3690 NEXT I
 3700 CLOSE #1
 3710 REM Read the Marriage Index
 3720 LOCATE 7,1 : PRINT "Open the Marriage Index"
 3730 OPEN CC.MINDEX$ FOR INPUT AS #2
 3740 INPUT #2, M.COUNT
 3750 IF M.COUNT <> 0 THEN 3800
 3760 PRINT "Marriage Index has no Index Records"
 3770 PRINT "Press any key to return to the Menu"
 3780 A$ = INKEY$ : IF A$ = "" THEN 3780
 3790 GOTO 9530
 3800 DIM PERS.NO(M.COUNT), M.NO(M.COUNT)
 3810 FOR I = 1 TO M.COUNT
 3820 LOCATE 8,1 : PRINT "Reading Marriage Index Record #:";I:
 3830  INPUT #2,PERS.NO(I), M.NO(I)
 3840 NEXT I
 3850 CLOSE #2
 3860 REM Open the Persons File
 3870 LOCATE 10,1 : PRINT "Open the Persons File"
 3880 OPEN CC.PERSFILE$ AS #1 LEN = 256
 3890 FIELD 1, 5 AS F1$, 20 AS F2$, 30 AS F3$, 2 AS F4$, 5 AS F5$, 5 AS F6$, 5 AS F7$, 11 AS F8$, 18 AS F9$, 16 AS F10$, 16 AS F11$, 11 AS F12$, 18 AS F13$, 16 AS F14$, 16 AS F15$, 11 AS F16$, 18 AS F17$, 16 AS F18$, 16 AS F19$
 3900 REM open the Marriages File
 3910 LOCATE 12,1 : PRINT "Open the Marriage File"
 3920 OPEN CC.MARRFILE$ AS #2 LEN = 128
 3930 FIELD 2, 5 AS M1$, 5 AS M2$, 5 AS M3$, 5 AS M4$, 11 AS M5$, 18 AS M6$, 16 AS M7$, 16 AS M8$, 45 AS M9$
 3940 REM Open the Ordinance File
 3950 IF DD.ORD$ = "no" THEN 3990
 3960 LOCATE 14,1 : PRINT "Open the Ordinances File";
 3970 OPEN CC.ORDFILE$ AS #3 LEN = 256
 3980 FIELD 3,5ASO1$,11ASO2$,11ASO3$,11ASO4$,5ASO5$,5ASO6$,11ASO7$,11ASO8$,11ASO9$,11ASO10$,11ASO11$,5ASO12$,11ASO13$,11ASO14$,11ASO15$,11ASO16$,11ASO17$,11ASO18$,11ASO19$,11ASO20$,11ASO21$,11ASO22$,11ASO23$,26ASO24$
 3990 REM Obtain a Person Record from the User
 4000 LOCATE 20,1 : PRINT SPACE$(79);: LOCATE 20,1
 4010 LINE INPUT "Enter the Record-number of a Person (0 to quit): ";REPLY$
 4020 IF REPLY$ = "0" THEN 9530
 4030 PERS(1) = VAL(REPLY$)
 4040 IF PERS(1) < 1 OR PERS(1) > MAX.PER THEN KEY ON : CLS : KEY OFF : LOCATE 19,1 : PRINT "Person Number is out of range"; : GOTO 3990
 4050 REM Obtain the information about a person
 4060 GET #1, PERS(1)
 4070 KEY ON : CLS
 4080 GOSUB 4660  'Extract Personal Information
 4090 GOSUB 2600 'Draw the form
 4100 LOCATE 2,30 : COLOR W : PRINT LEFT$(T3$ + " " + T2$,35);
 4110 LOCATE 2,66 : PRINT "Person:";T1
 4120 LOCATE 5,16 : COLOR G : PRINT T1
 4130 LOCATE 6,16 : PRINT T2$;
 4140 LOCATE 7,16 : PRINT LEFT$(T3$,24);
 4150 LOCATE 8,16 : PRINT T4$;
 4160 LOCATE 6,57 : PRINT T8$;
 4170 LOCATE 7,57 : PRINT T9$;
 4180 LOCATE 8,57 : PRINT T10$;
 4190 LOCATE 9,57 : PRINT T11$;
 4200 LOCATE 11,57 : PRINT T12$;
 4210 LOCATE 12,57 : PRINT T13$;
 4220 LOCATE 13,57 : PRINT T14$;
 4230 LOCATE 14,57 : PRINT T15$;
 4240 LOCATE 16,57 : PRINT T16$;
 4250 LOCATE 17,57 : PRINT T17$;
 4260 LOCATE 18,57 : PRINT T18$;
 4270 LOCATE 19,57 : PRINT T19$;
 4280 PERS(2) = T6
 4290 PERS(3) = T7
 4300 COLOR W
 4310 REM Check if Male Parent is known
 4320 IF PERS(2) = 0 THEN GOSUB 5000 : GOTO 4350
 4330 GET #1, PERS(2)
 4340 GOSUB 4660  'Extract
 4350 LOCATE 11,16 : COLOR G : PRINT T1;
 4360 LOCATE 12,16 : PRINT T2$;
 4370 LOCATE 13,16 : PRINT LEFT$(T3$,24);
 4380 LOCATE 14,16 : PRINT T8$;
 4390 COLOR W
 4400 REM Check if Female Parent is known
 4410 IF PERS(3) = 0 THEN GOSUB 5000 : GOTO 4440
 4420 GET #1, PERS(3)
 4430 GOSUB 4660  'Extract
 4440 LOCATE 17,16 : COLOR G : PRINT T1;
 4450 LOCATE 18,16 : PRINT T2$;
 4460 LOCATE 19,16 : PRINT LEFT$(T3$,24);
 4470 LOCATE 20,16 : PRINT T8$;
 4480 COLOR W,K : LOCATE 23,1 : PRINT SPACE$(79);
 4490 LOCATE 24,1 : PRINT SPACE$(79);
 4500 LOCATE 24,1 : PRINT "(Possible Requests:  ps, an, fg, ";
 4510 IF DD.ORD$ = "no" THEN 4530
 4520 PRINT "o, ";
 4530 PRINT "p1...pn, m1...mn, q)";
 4540 LOCATE 23,1
 4550 LINE INPUT "Type a Request.  Then press the 'enter' key.: "; REPLY$
 4560 IF REPLY$ = "ps" OR REPLY$ = "PS" THEN GOSUB 8130 : GOTO 4480
 4570 IF REPLY$ = "an" OR REPLY$ = "AN" THEN 5200  'Ancestors
 4580 IF REPLY$ = "pc" OR REPLY$ = "PC" THEN 5200  'Ancestors
 4590 IF REPLY$ = "fg" OR REPLY$ = "FG" THEN 6770  'Family Group
 4600 IF DD.ORD$ = "no" THEN 4620
 4610 IF REPLY$ = "o" OR REPLY$ = "O" THEN 8270  'Ordinances
 4620 IF LEFT$(REPLY$,1) = "p" OR LEFT$(REPLY$,1) = "P" THEN PERS(1) = VAL(RIGHT$(REPLY$,LEN(REPLY$)-1)) : KEY ON : CLS : KEY OFF : GOTO 4040
 4630 IF LEFT$(REPLY$,1) = "m" OR LEFT$(REPLY$,1) = "M" THEN 9400
 4640 IF LEFT$(REPLY$,1) = "q" OR LEFT$(REPLY$,1) = "Q" THEN 9530
 4650 LOCATE 22,1 : PRINT "Error in Previous Reply ";REPLY$; : GOTO 4480
 4660 REM Routine to Extract Personal Information
 4670 T1! = CVS(F1$) : T1 = T1!
 4680 T2$ = F2$
 4690 FOR J = 1 TO LEN(F2$) -1
 4700  IF RIGHT$(T2$,1)=" " THEN T2$ = LEFT$(T2$,LEN(T2$)-1) :ELSE J = LEN(F2$)-1
 4710 NEXT J
 4720 T3$ = F3$
 4730 FOR J = 1 TO LEN(F3$) -1
 4740  IF RIGHT$(T3$,1)=" " THEN T3$ = LEFT$(T3$,LEN(T3$)-1) :ELSE J = LEN(F3$)-1
 4750 NEXT J
 4760 T4$ = F4$
 4770 IF LEFT$(T4$,1) = MALE.LTR$   THEN T4$ = MALE.SEX$
 4780 IF LEFT$(T4$,1) = FEMALE.LTR$ THEN T4$ = FEMALE.SEX$
 4790 T6! = CVS(F6$) : T6 = T6!
 4800 T7! = CVS(F7$) : T7 = T7!
 4810 T8$ = F8$
 4820 T9$ = F9$
 4830 FOR J = 1 TO LEN(F9$) -1
 4840  IF RIGHT$(T9$,1)=" " THEN T9$ = LEFT$(T9$,LEN(T9$)-1) :ELSE J = LEN(F9$)-1
 4850 NEXT J
 4860 T10$ = F10$
 4870 T11$ = F11$
 4880 FOR J = 1 TO LEN(F11$) -1
 4890  IF RIGHT$(T11$,1)=" " THEN T11$ = LEFT$(T11$,LEN(T11$)-1) :ELSE J = LEN(F11$)-1
 4900 NEXT J
 4910 T12$ = F12$
 4920 T13$ = F13$
 4930 T14$ = F14$
 4940 T15$ = F15$
 4950 T16$ = F16$
 4960 T17$ = F17$
 4970 T18$ = F18$
 4980 T19$ = F19$
 4990 RETURN
 5000 REM Blank out a Record
 5010 T1 = 0
 5020 T2$ = ""
 5030 T3$ = ""
 5040 T4$ = ""
 5050 T6 = 0
 5060 T7 = 0
 5070 T8$ = ""
 5080 T9$ = ""
 5090 T10$ = ""
 5100 T11$ = ""
 5110 T12$ = ""
 5120 T13$ = ""
 5130 T14$ = ""
 5140 T15$ = ""
 5150 T16$ = ""
 5160 T17$ = ""
 5170 T18$ = ""
 5180 T19$ = ""
 5190 RETURN
 5200 REM Routine to Produce a Chart of Ancestors
 5210 KEY ON : CLS : KEY OFF
 5220 GOSUB 2000 'Draw the Chart
 5230 GET #1, PERS(1)
 5240 GOSUB 4660  'Extract the Person
 5250 LOCATE 2,17 : COLOR W : PRINT LEFT$(T3$ + " " + T2$,48);
 5260 LOCATE 2,66 : PRINT "Person:"; PERS(1);
 5270 THIS.PERS = PERS(1) : GOSUB 9580
 5280 LOCATE 12,6 : COLOR G : PRINT LEFT$(VALUE$,61);
 5290 LOCATE 12,68 : PRINT T8$;
 5300 PERS(2) = T6
 5310 PERS(3) = T7
 5320 REM Get 11
 5330 IF PERS(2) = 0 THEN GOSUB 5000 : GOTO 5390
 5340 GET #1, PERS(2)
 5350 GOSUB 4660  'Extract
 5360 THIS.PERS = PERS(2) : GOSUB 9580
 5370 LOCATE 8,16 : PRINT LEFT$(VALUE$,51);
 5380 LOCATE 8,68 : PRINT T8$;
 5390 PERS(4) = T6
 5400 PERS(5) = T7
 5410 REM Get 10
 5420 IF PERS(3) = 0 THEN GOSUB 5000 : GOTO 5480
 5430 GET #1, PERS(3)
 5440 GOSUB 4660  'Extract
 5450 THIS.PERS = PERS(3): GOSUB 9580
 5460 LOCATE 16,16 : PRINT LEFT$(VALUE$,51);
 5470 LOCATE 16,68 : PRINT T8$;
 5480 PERS(6) = T6
 5490 PERS(7) = T7
 5500 REM Get 111
 5510 IF PERS(4) = 0 THEN GOSUB 5000 : GOTO 5570
 5520 GET #1, PERS(4)
 5530 GOSUB 4660  'Extract
 5540 THIS.PERS = PERS(4): GOSUB 9580
 5550 LOCATE 6,26 : PRINT LEFT$(VALUE$,41);
 5560 LOCATE 6,68 : PRINT T8$;
 5570 PERS(8) = T6
 5580 PERS(9) = T7
 5590 REM Get 110
 5600 IF PERS(5) = 0 THEN GOSUB 5000 : GOTO 5660
 5610 GET #1, PERS(5)
 5620 GOSUB 4660  'Extract
 5630 THIS.PERS = PERS(5): GOSUB 9580
 5640 LOCATE 10,26 : PRINT LEFT$(VALUE$,41);
 5650 LOCATE 10,68 : PRINT T8$;
 5660 PERS(10) = T6
 5670 PERS(11) = T7
 5680 REM Get 101
 5690 IF PERS(6) = 0 THEN GOSUB 5000 : GOTO 5750
 5700 GET #1, PERS(6)
 5710 GOSUB 4660  'Extract
 5720 THIS.PERS = PERS(6): GOSUB 9580
 5730 LOCATE 14,26 : PRINT LEFT$(VALUE$,41);
 5740 LOCATE 14,68 : PRINT T8$;
 5750 PERS(12) = T6
 5760 PERS(13) = T7
 5770 REM Get 100
 5780 IF PERS(7) = 0 THEN GOSUB 5000 : GOTO 5840
 5790 GET #1, PERS(7)
 5800 GOSUB 4660  'Extract
 5810 THIS.PERS = PERS(7): GOSUB 9580
 5820 LOCATE 18,26 : PRINT LEFT$(VALUE$,41);
 5830 LOCATE 18,68 : PRINT T8$;
 5840 PERS(14) = T6
 5850 PERS(15) = T7
 5860 REM Get 1111
 5870 IF PERS(8) = 0 THEN GOSUB 5000 : GOTO 5930
 5880 GET #1, PERS(8)
 5890 GOSUB 4660  'Extract
 5900 THIS.PERS = PERS(8): GOSUB 9580
 5910 LOCATE  5,36 : PRINT LEFT$(VALUE$,31);
 5920 LOCATE  5,68 : PRINT T8$;
 5930 REM
 5940 REM Get 1110
 5950 IF PERS(9) = 0 THEN GOSUB 5000 : GOTO 6010
 5960 GET #1, PERS(9)
 5970 GOSUB 4660  'Extract
 5980 THIS.PERS = PERS(9): GOSUB 9580
 5990 LOCATE  7,36 : PRINT LEFT$(VALUE$,31);
 6000 LOCATE  7,68 : PRINT T8$;
 6010 REM
 6020 REM Get 1101
 6030 IF PERS(10) = 0 THEN GOSUB 5000 : GOTO 6090
 6040 GET #1, PERS(10)
 6050 GOSUB 4660  'Extract
 6060 THIS.PERS = PERS(10): GOSUB 9580
 6070 LOCATE  9,36 : PRINT LEFT$(VALUE$,31);
 6080 LOCATE  9,68 : PRINT T8$;
 6090 REM
 6100 REM Get 1100
 6110 IF PERS(11) = 0 THEN GOSUB 5000 : GOTO 6170
 6120 GET #1, PERS(11)
 6130 GOSUB 4660  'Extract
 6140 THIS.PERS = PERS(11): GOSUB 9580
 6150 LOCATE 11,36 : PRINT LEFT$(VALUE$,31);
 6160 LOCATE 11,68 : PRINT T8$;
 6170 REM
 6180 REM Get 1011
 6190 IF PERS(12) = 0 THEN GOSUB 5000 : GOTO 6250
 6200 GET #1, PERS(12)
 6210 GOSUB 4660  'Extract
 6220 THIS.PERS = PERS(12): GOSUB 9580
 6230 LOCATE 13,36 : PRINT LEFT$(VALUE$,31);
 6240 LOCATE 13,68 : PRINT T8$;
 6250 REM
 6260 REM Get 1010
 6270 IF PERS(13) = 0 THEN GOSUB 5000 : GOTO 6330
 6280 GET #1, PERS(13)
 6290 GOSUB 4660  'Extract
 6300 THIS.PERS = PERS(13): GOSUB 9580
 6310 LOCATE 15,36 : PRINT LEFT$(VALUE$,31);
 6320 LOCATE 15,68 : PRINT T8$;
 6330 REM
 6340 REM Get 1001
 6350 IF PERS(14) = 0 THEN GOSUB 5000 : GOTO 6410
 6360 GET #1, PERS(14)
 6370 GOSUB 4660  'Extract
 6380 THIS.PERS = PERS(14): GOSUB 9580
 6390 LOCATE 17,36 : PRINT LEFT$(VALUE$,31);
 6400 LOCATE 17,68 : PRINT T8$;
 6410 REM
 6420 REM Get 1000
 6430 IF PERS(15) = 0 THEN GOSUB 5000 : GOTO 6490
 6440 GET #1, PERS(15)
 6450 GOSUB 4660  'Extract
 6460 THIS.PERS = PERS(15): GOSUB 9580
 6470 LOCATE 19,36 : PRINT LEFT$(VALUE$,31);
 6480 LOCATE 19,68 : PRINT T8$;
 6490 COLOR W
 6500 LOCATE 23,1 : PRINT SPACE$(79);
 6510 LOCATE 24,1 : PRINT SPACE$(79);
 6520 LOCATE 24,1 : PRINT "(Possible Requests:  ps, an, fg,";
 6530 IF DD.ORD$ = "no" THEN 6550
 6540 PRINT " o,";
 6550 PRINT " l1...ln, p1...pn, m1...mn, q)";
 6560 LOCATE 23,1
 6570 LINE INPUT "Type a Request.  Then press the 'enter' key.: "; REPLY$
 6580 IF REPLY$ = "ps" OR REPLY$ = "PS" THEN GOSUB 8130 : GOTO 6500
 6590 IF REPLY$ = "an" OR REPLY$ = "AN" THEN 5200
 6600 IF REPLY$ = "pc" OR REPLY$ = "PC" THEN 5200
 6610 IF REPLY$ = "fg" OR REPLY$ = "FG" THEN 6770
 6620 IF DD.ORD$ = "no" THEN 6640
 6630 IF REPLY$ = "o"  OR REPLY$ = "O"  THEN 8270
 6640 IF LEFT$(REPLY$,1) = "l" OR LEFT$(REPLY$,1) = "L" THEN 6650 :ELSE 6730
 6650 WHO = (VAL(RIGHT$(REPLY$,LEN(REPLY$)-1)))
 6660 IF WHO < 1 OR WHO > 15 THEN 6670 :ELSE 6720
 6670 KEY ON : CLS : KEY OFF
 6680 LOCATE 20,1 : PRINT "Line-number is out of range"
 6690 LOCATE 22,1 : PRINT "Press any key to continue"
 6700 A$ = INKEY$ : IF A$ = "" THEN 6700
 6710 GOTO 4040
 6720 PERS(1) = PERS(WHO) : GOTO 4040
 6730 IF LEFT$(REPLY$,1) = "p" OR LEFT$(REPLY$,1) = "P" THEN PERS(1) = VAL(RIGHT$(REPLY$,LEN(REPLY$)-1)) : GOTO 4040
 6740 IF LEFT$(REPLY$,1) = "m" OR LEFT$(REPLY$,1) = "M" THEN 9400
 6750 IF LEFT$(REPLY$,1) = "q" OR LEFT$(REPLY$,1) = "Q" THEN 9530
 6760 LOCATE 22,1 : PRINT "Error in Previous Reply ";REPLY$; : GOTO 6500
 6770 REM Routine to Produce a Family Group
 6780 KEY ON : CLS
 6790 GOSUB 2970  'Draw the form
 6800 REM search the marriage index for the Person's Marriage
 6810 FOUND = 0
 6820 REM Establish Skip-ahead Start-value and Delta
 6830 START.AT = 1 : DELTA = INT(M.COUNT/10) : IF DELTA = 0 THEN 6880
 6840 REM Add delta and test if too far
 6850 START.AT = START.AT + DELTA
 6860 IF START.AT > 9 * DELTA THEN 6880
 6870 IF PERS(1) > PERS.NO(START.AT) THEN 6850
 6880 START.AT = START.AT - DELTA
 6890 REM Search Routine
 6900 FOR L = START.AT TO M.COUNT
 6910  IF PERS(1) > PERS.NO(L) THEN 6970
 6920  IF PERS(1) < PERS.NO(L) THEN L = M.COUNT : GOTO 6970
 6930  REM found a Marriage
 6940  FOUND = 1
 6950  GET #2, M.NO(L)
 6960  L = M.COUNT
 6970 NEXT L
 6980 IF FOUND = 1 THEN 7040
 6990 REM No marriage found
 7000 LOCATE 22,1 : COLOR W,K : PRINT "No Marriage Found";
 7010 LOCATE 23,1 : PRINT "Press any key to continue";
 7020 A$ = INKEY$ : IF A$ = "" THEN 7020
 7030 KEY ON : CLS : KEY OFF : GOTO 4050
 7040 REM extract Information from the Marriage Record
 7050 TT1! = CVS(M1$) : TT1 = TT1!
 7060 IF TT1 < 1 THEN 6990
 7070 TT2! = CVS(M2$) : TT2 = TT2!
 7080 TT3! = CVS(M3$) : TT3 = TT3!
 7090 TT5$ = M5$
 7100 TT6$ = M6$
 7110 REM Right-trim
 7120 FOR J = 1 TO LEN(M6$) -1
 7130  IF RIGHT$(TT6$,1)=" " THEN TT6$ = LEFT$(TT6$,LEN(TT6$)-1) :ELSE J = LEN(M6$)-1
 7140 NEXT J
 7150 TT7$ = M7$
 7160 TT8$ = M8$
 7170 REM Right-trim
 7180 FOR J = 1 TO LEN(M8$) -1
 7190  IF RIGHT$(TT8$,1)=" " THEN TT8$ = LEFT$(TT8$,LEN(TT8$)-1) :ELSE J = LEN(M8$)-1
 7200 NEXT J
 7210 TT9$ = M9$
 7220 REM print the Marriage Information
 7230 LOCATE 2,73 : COLOR W : PRINT TT1
 7240 LOCATE 7,18 : COLOR G : PRINT TT5$
 7250 IF TT6$ = " " AND TT8$ = " " THEN 7270
 7260 LOCATE 7,45 : PRINT LEFT$(TT6$+", "+TT8$,34)
 7270 REM get the Husband's Record
 7280 GET #1, TT2
 7290 GOSUB 4660  'Extract
 7300 THIS.PERS = TT2 : GOSUB 9580
 7310 LOCATE 4,11 : COLOR G : PRINT LEFT$(VALUE$,44);
 7320 LOCATE 4,67 : PRINT T8$;
 7330 REM get the Wife's Record
 7340 GET #1, TT3
 7350 GOSUB 4660  'Extract
 7360 THIS.PERS = TT3 : GOSUB 9580
 7370 LOCATE 5,11 : COLOR G : PRINT LEFT$(VALUE$,44);
 7380 LOCATE 5,67 : PRINT T8$;
 7390 REM Blank previous children and find new ones
 7400 FOR IC = 1 TO CHILD.COUNT
 7410  CH(IC) = 0
 7420 NEXT IC
 7430 CHILD.COUNT = 0
 7440 REM search the parent/child index
 7450 REM Establish Skip-ahead Start-value and Delta
 7460 START.AT = 1 : DELTA = INT(PC.COUNT/10) : IF DELTA = 0 THEN 7510
 7470 REM Add delta and test if too far
 7480 START.AT = START.AT + DELTA
 7490 IF START.AT > 9 * DELTA THEN 7510
 7500 IF HUSB > PA.ID(START.AT) THEN 7480
 7510 START.AT = START.AT - DELTA
 7520 REM Search Routine
 7530 FOR LL = START.AT TO PC.COUNT
 7540  IF TT2 > PA.ID(LL) THEN 7930
 7550  IF TT2 < PA.ID(LL) THEN LL = PC.COUNT : GOTO 7930
 7560  REM found a child
 7570  GET #1, CH.ID(LL)
 7580  GOSUB 4660  'Extract
 7590  REM verify that Mother is the same
 7600  IF TT3 <> T7 THEN 7930
 7610  REM Found a valid child
 7620  CHILD.COUNT = CHILD.COUNT + 1
 7630  SHOW.COUNT = CHILD.COUNT
 7640  IF CHILD.COUNT = 1 THEN 7810
 7650  X11 = 0
 7660  X11 = X11 + 11
 7670  IF CHILD.COUNT > X11 THEN SHOW.COUNT = CHILD.COUNT - X11 : GOTO 7660
 7680  IF (CHILD.COUNT-1) MOD 11 = 0 THEN 7690 :ELSE 7810
 7690  LOCATE 23,1 : PRINT SPACE$(79);
 7700  LOCATE 23,1
 7710  PRINT "Press p to Print Screen, or any other key to continue"
 7720  A$ = INKEY$ : IF A$ = "" THEN 7720
 7730  IF A$ = "P" OR A$ = "p" THEN GOSUB 8130 : GOTO 7720
 7740  REM blank the previous children
 7750  FOR ROW = 9 TO 20
 7760   LOCATE ROW,2 : PRINT SPACE$(77);
 7770  NEXT ROW
 7780  REM restore the rest of the display
 7790  GOSUB 3110
 7800  LOCATE 23,1 : PRINT SPACE$(79)
 7810  CH(CHILD.COUNT) = CH.ID(LL)
 7820  LOCATE 9+SHOW.COUNT,2 : COLOR K,W
 7830  CC.STR$ = " "+STR$(CHILD.COUNT)
 7840  PRINT RIGHT$(CC.STR$,3); : COLOR G,K
 7850  LOCATE 9+SHOW.COUNT,6 : PRINT LEFT$(F4$,1);  'Sex
 7860  NM$ = T2$+", "+T3$
 7870  IF T2$ = " " OR T3$ = " " THEN NM$ = T2$+T3$
 7880  LOCATE 9+SHOW.COUNT,8 : PRINT LEFT$(NM$,32);
 7890  LOCATE 9+SHOW.COUNT,41 : PRINT T8$;
 7900  IF T9$ = " " AND T11$ = " " THEN 7920
 7910  LOCATE 9+SHOW.COUNT,53 : PRINT LEFT$(T9$+", "+T11$,26);
 7920  COLOR W,K
 7930 NEXT LL
 7940 LOCATE 23,1 : PRINT SPACE$(79);
 7950 LOCATE 24,1 : PRINT SPACE$(79);
 7960 LOCATE 24,1 : PRINT "(Possible Requests: ps, f, m, p1...pn, c1...cn, m1...mn, q)";
 7970 LOCATE 23,1
 7980 LINE INPUT "Type a Request.  Then press the 'enter' key.: "; REPLY$
 7990 IF REPLY$ = "ps" OR REPLY$ = "PS" THEN GOSUB 8130 : GOTO 7940
 8000 IF REPLY$ = "an" OR REPLY$ = "AN" THEN LOCATE 22,1 : PRINT "Error in Previous Reply ";REPLY$; : GOTO 7940
 8010 IF REPLY$ = "pc" OR REPLY$ = "PC" THEN LOCATE 22,1 : PRINT "Error in Previous Reply ";REPLY$; : GOTO 7940
 8020 IF REPLY$ = "f" OR REPLY$ = "F" THEN PERS(1) = TT2 : GOTO 4050
 8030 IF REPLY$ = "m" OR REPLY$ = "M" THEN PERS(1) = TT3 : GOTO 4050
 8040 IF LEFT$(REPLY$,1) = "p" OR LEFT$(REPLY$,1) = "P" THEN PERS(1) = VAL(RIGHT$(REPLY$,LEN(REPLY$)-1)) : GOTO 4040
 8050 IF LEFT$(REPLY$,1) = "c" OR LEFT$(REPLY$,1) = "C" THEN 8060 :ELSE 8100
 8060 CHLD = VAL(RIGHT$(REPLY$,LEN(REPLY$)-1))
 8070 IF CHLD < 1 OR CHLD > CHILD.COUNT THEN 8120
 8080 PERS(1) = CH(CHLD)
 8090 GOTO 4050  'for personal
 8100 IF LEFT$(REPLY$,1) = "m" OR LEFT$(REPLY$,1) = "M" THEN 9400 'marriage
 8110 IF LEFT$(REPLY$,1) = "q" OR LEFT$(REPLY$,1) = "Q" THEN 9530
 8120 LOCATE 22,1 : PRINT "Error in Previous Reply ";REPLY$; : GOTO 7940
 8130 REM Routine to Print the Screen
 8140 REM Accessed by users 'ps' reply
 8150 LPRINT : LPRINT : LPRINT : LPRINT
 8160 FOR ROW = 1 TO 21
 8170  FOR COL = 1 TO 79
 8180   X = SCREEN(ROW,COL)
 8190   IF PTR.SCRN <> 0 THEN 8210
 8200   IF X < 32 OR X > 125 THEN X = 32
 8210   LPRINT CHR$(X);
 8220  NEXT COL
 8230  LPRINT
 8240 NEXT ROW
 8250 LPRINT FORM.FEED$;
 8260 RETURN
 8270 REM Routine to Display the Ordinances
 8280 KEY ON : CLS
 8290 GET #1, PERS(1) : GOSUB 4660
 8300 GET #3, PERS(1)
 8310 SEX$ = " "
 8320 IF T4$ = MALE.SEX$   THEN SEX$ = MALE.LTR$
 8330 IF T4$ = FEMALE.SEX$ THEN SEX$ = FEMALE.LTR$
 8340 REM Extract the Ordinance Information
 8350 U1! = CVS(O1$) : U1 = U1!
 8360 REM Blank Ordinances of no Ordinance Record Present
 8370 IF U1 = 0 THEN GOSUB 9690 : GOTO 8610
 8380 U2$ = O2$
 8390 U3$ = O3$
 8400 U4$ = O4$
 8410 U5! = CVS(O5$) : U5 = U5!
 8420 U6! = CVS(O6$) : U6 = U6!
 8430 U7$ = O7$
 8440 U8$ = O8$
 8450 U9$ = O9$
 8460 U10$ = O10$
 8470 U11$ = O11$
 8480 U12! = CVS(O12$) : U12 = U12!
 8490 U13$ = O13$
 8500 U14$ = O14$
 8510 U15$ = O15$
 8520 U16$ = O16$
 8530 U17$ = O17$
 8540 U18$ = O18$
 8550 U19$ = O19$
 8560 U20$ = O20$
 8570 U21$ = O21$
 8580 U22$ = O22$
 8590 U23$ = O23$
 8600 U24$ = O24$
 8610 KEY OFF
 8620 R1 = 1 : C1 = 1 : R2 = 21 : C2 = 79 : GOSUB 1110  'Double Box
 8630 R1 = 3 : C1 = 1 : R2 = 3 : C2 = 79 : GOSUB 3210  'Horizontal Double
 8640 R1 = 19 : C1 = 1 : R2 = 19 : C2 = 79 : GOSUB 3210  'Horizontal Double
 8650 LOCATE 2,3 : PRINT "Ordinances of:";
 8660 LOCATE 4,7 : COLOR N : PRINT "Personal Record"; : COLOR O
 8670 LOCATE 5,3 : PRINT "Christening:";
 8680 LOCATE 6,3 : PRINT "Blessing:";
 8690 LOCATE 7,3 : PRINT "Sealed to Parents:";
 8700 LOCATE 8,5 : PRINT "Father's Rec.no:";
 8710 LOCATE 9,6 : PRINT "Name:";
 8720 LOCATE 10,5 : PRINT "Mother's Rec.no:";
 8730 LOCATE 11,6 : PRINT "Name:";
 8740 LOCATE 12,3 : PRINT "Baptism:";
 8750 LOCATE 13,3 : PRINT "Confirmation:";
 8760 LOCATE 14,3 : PRINT "Patriarchal Blessing:";
 8770 LOCATE 15,3 : PRINT "Endowment:";
 8780 REM Test for male.  Skip if male.
 8790 IF SEX$ = MALE.LTR$ THEN 8830
 8800 LOCATE 16,3 : PRINT "Sealed to Husband:";
 8810 LOCATE 17,5 : PRINT "Husband's Rec.no:";
 8820 LOCATE 18,6 : PRINT "Name:";
 8830 REM Test for Male.  Skip if not
 8840 IF SEX$ <> MALE.LTR$ THEN 8980
 8850 R1 = 3 : R2 = 19 : C1 = 51 : C2 = 51 : GOSUB 3350  'Vertical Double
 8860 LOCATE 4,57 : COLOR N : PRINT "Priesthood Record"; : COLOR O
 8870 LOCATE 5,53 : PRINT "Aaronic:";
 8880 LOCATE 6,55 : PRINT "Deacon:";
 8890 LOCATE 7,55 : PRINT "Teacher:";
 8900 LOCATE 8,55 : PRINT "Priest:";
 8910 LOCATE 10,53 : PRINT "Melchizedek:";
 8920 LOCATE 11,55 : PRINT "Elder:";
 8930 LOCATE 12,55 : PRINT "Seventy:";
 8940 LOCATE 13,55 : PRINT "High Priest:";
 8950 LOCATE 15,53 : PRINT "Bishop:";
 8960 LOCATE 16,53 : PRINT "Patriarch:";
 8970 LOCATE 17,53 : PRINT "Apostle:";
 8980 LOCATE 20,3  : PRINT "Occupation:";
 8990 REM Print the Information Currently Present
 9000 LOCATE 2,18 : COLOR W : PRINT LEFT$(T3$ + " " + T2$,47);
 9010 LOCATE 2,66 : PRINT "Rec.no:";T1;
 9020 LOCATE 5,28 : COLOR G : PRINT U2$;
 9030 LOCATE 6,28 : PRINT U3$;
 9040 LOCATE 7,28 : PRINT U4$;
 9050 IF SEX$ = MALE.LTR$ THEN NO.SP = 38 :ELSE NO.SP = 51
 9060 LOCATE 8,27 : PRINT U5;
 9070 IF U5 = 0 THEN 9100
 9080 GET #1, U5 : GOSUB 4660  'Extract Father Information
 9090 LOCATE 9,12 : PRINT LEFT$(T3$ + " " + T2$,NO.SP);
 9100 LOCATE 10,27 : PRINT U6;
 9110 IF U6 = 0 THEN 9140
 9120 GET #1, U6 : GOSUB 4660  'Extract Mother Information
 9130 LOCATE 11,12 : PRINT LEFT$(T3$ + " " + T2$,NO.SP);
 9140 LOCATE 12,28 : PRINT U7$;
 9150 LOCATE 13,28 : PRINT U8$;
 9160 LOCATE 14,28 : PRINT U9$;
 9170 LOCATE 15,28 : PRINT U10$;
 9180 REM Test for male.  Skip if male.
 9190 IF SEX$ = MALE.LTR$ THEN 9250
 9200 LOCATE 16,28 : PRINT U11$;
 9210 LOCATE 17,27 : PRINT U12;
 9220 IF U12 = 0 THEN 9250
 9230 GET #1, U12 : GOSUB 4660  'Extract Spouse Information
 9240 LOCATE 18,12 : PRINT T3$ + " " + T2$;
 9250 REM Test for Male.  Bypass if not.
 9260 IF SEX$ <> MALE.LTR$ THEN 9380
 9270 LOCATE 5,67 : PRINT U13$;
 9280 LOCATE 6,67 : PRINT U14$;
 9290 LOCATE 7,67 : PRINT U15$;
 9300 LOCATE 8,67 : PRINT U16$;
 9310 LOCATE 10,67 : PRINT U17$;
 9320 LOCATE 11,67 : PRINT U18$;
 9330 LOCATE 12,67 : PRINT U19$;
 9340 LOCATE 13,67 : PRINT U20$;
 9350 LOCATE 15,67 : PRINT U21$;
 9360 LOCATE 16,67 : PRINT U22$;
 9370 LOCATE 17,67 : PRINT U23$;
 9380 LOCATE 20,15 : PRINT U24$;
 9390 GOTO 4480  'For User Action
 9400 REM Marriage was requested by Number
 9410 MARRIAGE = VAL(RIGHT$(REPLY$,LEN(REPLY$)-1))
 9420 KEY ON : CLS : KEY OFF
 9430 IF MARRIAGE > 0 AND MARRIAGE <= MAX.MAR THEN 9500
 9440 LOCATE 22,1 : PRINT SPACE$(79);
 9450 LOCATE 22,1 : PRINT "Marriage Number is out of range";
 9460 LOCATE 23,1 : PRINT SPACE$(79);
 9470 LOCATE 23,1 : PRINT "Press any key to continue."
 9480 A$ = INKEY$ : IF A$ = "" THEN 9480
 9490 GOTO 4050
 9500 GOSUB 2970  'Print the form
 9510 GET #2, MARRIAGE
 9520 GOTO 7040
 9530 REM Wrapup
 9540 CLOSE
 9550 KEY ON : CLS : KEY OFF : LOCATE 21,1 : COLOR W,K
 9560 PRINT "End of Program"
 9570 RUN CC.MENU$
 9580 REM Routine to Convert a number to a string.  This.pers is input
 9590 REM value$ is output, with record number and persons name.
 9600 VALUE$ = STR$(THIS.PERS)
 9610 PLEN = LEN(VALUE$)
 9620 VALUE$ = RIGHT$(VALUE$,PLEN-1)
 9630 NM$ = T2$+", "+T3$
 9640 IF T2$ = " " OR  T3$ = " " THEN NM$ = T2$+T3$
 9650 IF T2$ = " " AND T3$ = " " THEN NM$ = ""
 9660 IF CHART.NOS$ <> "n" THEN VALUE$ = NM$ : GOTO 9680
 9670 VALUE$ = "("+VALUE$+") "+NM$
 9680 RETURN
 9690 REM Blank Ordinances if No Ord Record
 9700 U2$  = "" : U3$  = "" : U4$  = ""
 9710 U5   = 0  : U6   = 0  : U12  = 0
 9720 U7$  = "" : U8$  = "" : U9$  = "" : U10$ = ""
 9730 U11$ = "" : U13$ = "" : U14$ = "" : U15$ = ""
 9740 U16$ = "" : U17$ = "" : U18$ = "" : U19$ = ""
 9750 U20$ = "" : U21$ = "" : U22$ = "" : U23$ = ""
 9760 U24$ = ""
 9770 RETURN
