  100 REM UPDATPER Program.
  110 REM Data Entry to the Persons File
  120 REM Copyright (c) 1982 ... 1989 by: Melvin O. Duke.
  130 OPTION BASE 0
  140 DEFINT A-Z
  600 REM Titles
  610 TITLE$ = "Update the Persons File"
  620 TITLE$ = TITLE$ + " ON DISPLAY"
  700 REM Terminate if not called from the Menu
  710 IF COPY2$ = "Melvin O. Duke" THEN 770
  720 COLOR 7,0 : KEY ON : CLS : LOCATE 15,1
  730 PRINT "Cannot run the"
  740 PRINT TITLE$
  750 PRINT "Program, unless selected from the MENU"
  760 END
  770 REM OK
 1000 REM Produce the first screen
 1010 KEY ON : CLS : KEY OFF
 1040 REM Find the title location
 1050 TITLE.POS = 40 - INT(LEN(TITLE$)/2)
 1080 REM Print the title
 1090 LOCATE 4,TITLE.POS : PRINT TITLE$
 1100 LOCATE 5,40-INT(LEN(VERSION$)/2) :  PRINT VERSION$;
 1250 REM Print the Copyright
 1260 LOCATE 20,40-INT(LEN(COPY1$)/2) :  PRINT COPY1$;
 1270 LOCATE 21,40-INT(LEN(COPY2$)/2) :  PRINT COPY2$;
 1280 GOTO 1700
 1300 REM subroutine to print a double box
 1310 COLOR P
 1320 FOR I = R1 + 1 TO R2 - 1
 1330  LOCATE I, C1 : PRINT CHR$(186);
 1340  LOCATE I, C2 : PRINT CHR$(186);
 1350 NEXT I
 1360  LOCATE R1, C1+1 : PRINT STRING$(C2-C1-1,205);
 1390  LOCATE R2, C1+1 : PRINT STRING$(C2-C1-1,205);
 1400  LOCATE R1, C1 : PRINT CHR$(201);
 1410  LOCATE R1, C2 : PRINT CHR$(187);
 1420  LOCATE R2, C1 : PRINT CHR$(200);
 1430  LOCATE R2, C2 : PRINT CHR$(188);
 1440 COLOR W
 1450 RETURN
 1700 REM Display the Copyright
 1710 '
 1720 LOCATE 25,1
 1730 rem PRINT DATADISK$;
 1740 rem K$ = INKEY$ : IF K$ = "" THEN 1740
 1750 KEY ON : CLS : KEY OFF
 2000 REM UPDATPER Program Starts Here.
 2010 OPEN CC.PERSFILE$ AS #1 LEN = 256
 2020 FIELD 1, 5 AS F1$, 20 AS F2$, 30 AS F3$, 2 AS F4$, 5 AS F5$, 5 AS F6$, 5 AS F7$, 11 AS F8$, 18 AS F9$, 16 AS F10$, 16 AS F11$, 11 AS F12$, 18 AS F13$, 16 AS F14$, 16 AS F15$, 11 AS F16$, 18 AS F17$, 16 AS F18$, 16 AS F19$
 2030 REM ask the user for input
 2040 LOCATE 23,1 : PRINT SPACE$(79);
 2050 LOCATE 23,1 : PRINT "(0 to quit, ? to locate unused record)";
 2060 LOCATE 22,1 : PRINT SPACE$(79) : LOCATE 22,1
 2070 LINE INPUT "Enter Record Number of Person to Update: "; REPLY$
 2080 IF REPLY$ <> "?" THEN 2240
 2090 REM Locate an unused record
 2100 FOUND = 0 : IF REC.NO = 0 THEN REC.NO = 1
 2110 FOR LOOK = REC.NO TO MAX.PER
 2120  GET #1, LOOK
 2130  LOCATE 15,1 : PRINT "Searching Record";LOOK;
 2140  T1 = CVS(F1$)
 2150  IF T1 > 0 THEN 2170
 2160  FOUND = 1 : REC.NO = LOOK : LOOK = MAX.PER
 2170 NEXT LOOK
 2180 IF FOUND = 1 THEN 2330
 2190 PRINT "Unable to find an unused record above record";REC.NO
 2200 PRINT "Either start from record 1 or extend the file"
 2210 PRINT "Press any key to continue"
 2220 A$ = INKEY$ : IF A$ = "" THEN 2220
 2230 GOTO 2030
 2240 IF REPLY$ = "0" THEN 6330
 2250 REC.NO = VAL(REPLY$)
 2260 IF REC.NO < 1 OR REC.NO > MAX.PER THEN 2270 :ELSE 2320
 2270 PRINT : PRINT "Number is out of range"
 2280 PRINT "Press any key to continue"
 2290 A$ = INKEY$ : IF A$ = "" THEN 2290
 2300 KEY ON : CLS : KEY OFF
 2310 GOTO 2030
 2320 GET #1, REC.NO
 2330 REM Extract information from the file for use
 2340 T1! = CVS(F1$) : T1 = T1!
 2350 REM Disallow Update if Rec.no is Zero (never Created)
 2360 IF T1 <> 0 THEN 2420
 2370 LOCATE 22,1 : PRINT SPACE$(79);: LOCATE 23,1 : PRINT SPACE$(79);: LOCATE 22,1
 2380 PRINT "Record Number is Zero.  Must run the CREATPER Program First."
 2390 KEY OFF : LOCATE 25,1 : PRINT "Press any key to continue";
 2400 A$ = INKEY$ : IF A$ = "" THEN 2400
 2410 GOTO 6330  'Close the Files and return to the Menu
 2420 T2$ = F2$          'Surname
 2430 T3$ = F3$          'Given Names
 2440 FOR J = 1 TO LEN(F3$)-1
 2450  IF RIGHT$(T3$,1)=" "THEN T3$ = LEFT$(T3$,LEN(T3$)-1) :ELSE J = LEN(F3$)-1
 2460 NEXT J
 2470 T4$ = F4$          'Sex
 2480 IF LEFT$(T4$,1) = MALE.LTR$    THEN T4$ = MALE.SEX$
 2490 IF LEFT$(T4$,1) = FEMALE.LTR$  THEN T4$ = FEMALE.SEX$
 2500 T5! = CVS(F5$)                'Code
 2510 T6! = CVS(F6$) : T6 = T6!     'Father's No.
 2520 T7! = CVS(F7$) : T7 = T7!     'Mother's No.
 2530 T8$ = F8$          'Birthdate
 2540 T9$ = F9$
 2550 T10$ = F10$
 2560 T11$ = F11$
 2570 T12$ = F12$        'Death Date
 2580 T13$ = F13$
 2590 T14$ = F14$
 2600 T15$ = F15$
 2610 T16$ = F16$        'Burial Date
 2620 T17$ = F17$
 2630 T18$ = F18$
 2640 T19$ = F19$
 2650 KEY ON : CLS : KEY OFF
 2660 R1 = 1 : C1 = 1 : R2 = 21 : C2 = 79 : GOSUB 1300  'Double box
 2670 R1 = 3 : C1 = 1 : R2 = 3 : C2 = 79 : GOSUB 3750  'Horizontal double
 2680 LOCATE 2,3 : PRINT "Personal Information of:"
 2690 R1 = 3 : C1 = 43 : R2 = 21 : C2 = 43 : GOSUB 3890  'Vertical Double
 2700 LOCATE 4,3 : COLOR N : PRINT "Person:"; : COLOR O
 2710 LOCATE 5,3 : PRINT "Record-number:";
 2720 LOCATE 7,3 : PRINT "Surname:";
 2730 LOCATE 9,3 : PRINT "Given-names:";
 2740 LOCATE 11,3 : PRINT "Sex:";
 2750 IF CODE$ = "no" THEN 2770
 2760 LOCATE 12,3 : PRINT "Code:";
 2770 R1 = 13 : C1 = 1 : R2 =13 : C2 = 43 : GOSUB 3750  'Horizontal Double
 2780 LOCATE 14,3 : COLOR N : PRINT "Person's Father:"; : COLOR O
 2790 LOCATE 15,3 : PRINT "Record-number:";
 2800 LOCATE 16,3 : PRINT "Name:";
 2810 R1 = 17 : C1 = 1 : R2 = 17 : C2 = 43 : GOSUB 3820  'Horizontal Single
 2820 LOCATE 18,3 : COLOR N : PRINT "Person's Mother:"; : COLOR O
 2830 LOCATE 19,3 : PRINT "Record-number:";
 2840 LOCATE 20,3 : PRINT "Name:";
 2850 LOCATE 4,45 : COLOR N : PRINT "Person's Vital Statistics:"; : COLOR O
 2860 LOCATE 6,45 : PRINT "Birth-date:";
 2870 LOCATE 7,45 : PRINT "Birth-city:";
 2880 LOCATE 8,45 : PRINT "Birth-county:";
 2890 LOCATE 9,45 : PRINT "State/Country:";
 2900 LOCATE 11,45 : PRINT "Death-date:";
 2910 LOCATE 12,45 : PRINT "Death-city:";
 2920 LOCATE 13,45 : PRINT "Death-county:";
 2930 LOCATE 14,45 : PRINT "State/Country:";
 2940 LOCATE 16,45 : PRINT "Burial-date:";
 2950 LOCATE 17,45 : PRINT "Burial-city:";
 2960 LOCATE 18,45 : PRINT "Burial-county:";
 2970 LOCATE 19,45 : PRINT "State/Country:";
 2980 GOSUB 3000 'To print the current information
 2990 GOTO 3980 'For User Input
 3000 REM Print the Information Currently Present
 3010 LOCATE 2,28 : PRINT SPACE$(50); : COLOR W
 3020 LOCATE 2,28 : PRINT LEFT$(T3$ + " " + T2$,50);
 3030 LOCATE 6,8 : PRINT SPACE$(5);
 3040 LOCATE 6,8 : COLOR G : PRINT T1;
 3050 LOCATE 8,9 : PRINT SPACE$(20);
 3060 LOCATE 8,9 : COLOR G : PRINT LEFT$(T2$,20);
 3070 LOCATE 10,9 : PRINT SPACE$(30);
 3080 LOCATE 10,9 : COLOR G : PRINT LEFT$(T3$,30);
 3090 LOCATE 11,9 : PRINT SPACE$(20);
 3100 LOCATE 11,9 : COLOR G : PRINT LEFT$(T4$,20);
 3110 IF CODE$ = "no" THEN 3140
 3120 LOCATE 12,8 : PRINT SPACE$(15);
 3130 LOCATE 12,8 : COLOR G : PRINT T5!;
 3140 LOCATE 15,18 : PRINT SPACE$(5);
 3150 LOCATE 15,18 : COLOR G : PRINT T6;
 3160 REM Obtain the Father's Record
 3170 IF T6 = 0 THEN SN$ = " " : GN$ = " " : GOTO 3270
 3180 GET #1, T6
 3190 SN$ = F2$ : GN$ = F3$
 3200 REM right-trim the names
 3210 FOR J = 1 TO LEN(F2$)-1
 3220  IF RIGHT$(SN$,1)=" "THEN SN$=LEFT$(SN$,LEN(SN$)-1) :ELSE J=LEN(F2$)-1
 3230 NEXT J
 3240 FOR J = 1 TO LEN(F3$)-1
 3250  IF RIGHT$(GN$,1)=" "THEN GN$=LEFT$(GN$,LEN(GN$)-1) :ELSE J=LEN(F3$)-1
 3260 NEXT J
 3270 NM$ = SN$ + ", " + GN$
 3280 IF SN$ = " " OR  GN$ = " " THEN NM$ = SN$ + " " + GN$
 3290 IF SN$ = " " AND GN$ = " " THEN NM$ = " "
 3300 LOCATE 16, 9 : PRINT SPACE$(33);
 3310 LOCATE 16, 9 : COLOR G : PRINT LEFT$(NM$,33);
 3320 LOCATE 19,18 : PRINT SPACE$(5);
 3330 LOCATE 19,18 : COLOR G : PRINT T7;
 3340 REM Obtain the Mother's Record
 3350 IF T7 = 0 THEN SN$ = " " : GN$ = " " : GOTO 3450
 3360 GET #1, T7
 3370 SN$ = F2$ : GN$ = F3$
 3380 REM right-trim the names
 3390 FOR J = 1 TO LEN(F2$)-1
 3400  IF RIGHT$(SN$,1)=" "THEN SN$=LEFT$(SN$,LEN(SN$)-1) :ELSE J=LEN(F2$)-1
 3410 NEXT J
 3420 FOR J = 1 TO LEN(F3$)-1
 3430  IF RIGHT$(GN$,1)=" "THEN GN$=LEFT$(GN$,LEN(GN$)-1) :ELSE J=LEN(F3$)-1
 3440 NEXT J
 3450 NM$ = SN$ + ", " + GN$
 3460 IF SN$ = " " OR  GN$ = " " THEN NM$ = SN$ + " " + GN$
 3470 IF SN$ = " " AND GN$ = " " THEN NM$ = " "
 3480 LOCATE 20, 9 : PRINT SPACE$(33);
 3490 LOCATE 20, 9 : COLOR G : PRINT LEFT$(NM$,33);
 3500 LOCATE 6,60 : PRINT SPACE$(11);
 3510 LOCATE 6,60 : COLOR G : PRINT T8$;
 3520 LOCATE 7,60 : PRINT SPACE$(18);
 3530 LOCATE 7,60 : COLOR G : PRINT T9$;
 3540 LOCATE 8,60 : PRINT SPACE$(16);
 3550 LOCATE 8,60 : COLOR G : PRINT T10$;
 3560 LOCATE 9,60 : PRINT SPACE$(16);
 3570 LOCATE 9,60 : COLOR G : PRINT T11$;
 3580 LOCATE 11,60 : PRINT SPACE$(11);
 3590 LOCATE 11,60 : COLOR G : PRINT T12$;
 3600 LOCATE 12,60 : PRINT SPACE$(18);
 3610 LOCATE 12,60 : COLOR G : PRINT T13$;
 3620 LOCATE 13,60 : PRINT SPACE$(16);
 3630 LOCATE 13,60 : COLOR G : PRINT T14$;
 3640 LOCATE 14,60 : PRINT SPACE$(16);
 3650 LOCATE 14,60 : COLOR G : PRINT T15$;
 3660 LOCATE 16,60 : PRINT SPACE$(11);
 3670 LOCATE 16,60 : COLOR G : PRINT T16$;
 3680 LOCATE 17,60 : PRINT SPACE$(18);
 3690 LOCATE 17,60 : COLOR G : PRINT T17$;
 3700 LOCATE 18,60 : PRINT SPACE$(16);
 3710 LOCATE 18,60 : COLOR G : PRINT T18$;
 3720 LOCATE 19,60 : PRINT SPACE$(16);
 3730 LOCATE 19,60 : COLOR G : PRINT T19$; : COLOR W
 3740 RETURN
 3750 REM Subroutine to draw a double horizontal line.  Attach to double.
 3760 COLOR P
 3770 LOCATE R1, C1+1 : PRINT STRING$(C2-C1-1,205);
 3780 LOCATE R1,C1 : PRINT CHR$(204);
 3790 LOCATE R1,C2 : PRINT CHR$(185);
 3800 COLOR W
 3810 RETURN
 3820 REM Subroutine to draw a single horizontal line.  Attach to double.
 3830 COLOR P
 3840 LOCATE R1, C1+1 : PRINT STRING$(C2-C1-1,196);
 3850 LOCATE R1,C1 : PRINT CHR$(199);
 3860 LOCATE R1,C2 : PRINT CHR$(182);
 3870 COLOR W
 3880 RETURN
 3890 REM Subroutine to draw a double vertical line.  Attach to double.
 3900 COLOR P
 3910 FOR I = R1 + 1 TO R2 - 1
 3920  LOCATE I,C1 : PRINT CHR$(186);
 3930 NEXT I
 3940 LOCATE R1,C1 : PRINT CHR$(203);
 3950 LOCATE R2,C1 : PRINT CHR$(202);
 3960 COLOR W
 3970 RETURN
 3980 REM Routines to Obtain information from the User
 3990 LOCATE 22,1 : PRINT SPACE$(79);
 4000 LOCATE 23,1 : PRINT SPACE$(79);
 4010 LOCATE 24,1 : PRINT SPACE$(79);
 4020 KEY ON
 4030 LOCATE 24,1 : PRINT "('enter' to leave alone, '/ enter' to end record, or reply as shown.)";
 4040 LOCATE 23,1
 4050 LINE INPUT "Enter the Record Number: ";REPLY$
 4060 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4070 IF REPLY$ = "/" THEN 5970
 4080 IF REPLY$ = "" THEN 4190
 4090 IF ABS(VAL(REPLY$)) = ABS(T1) THEN 4160 :ELSE 4100
 4100 REM Prevent Change of Rec.no
 4110 LOCATE 22,1 : PRINT SPACE$(79); : LOCATE 24,1 : PRINT SPACE$(79); : LOCATE 22,1
 4120 PRINT "Cannot Change the Record Number to another number.";
 4130 KEY OFF : LOCATE 25,1 : PRINT "Press any key to continue";
 4140 A$ = INKEY$ : IF A$ = "" THEN 4140
 4150 GOTO 3980
 4160 T1 = VAL(REPLY$)
 4170 IF T1 < 1 THEN GOSUB 6370 : GOSUB 3000 : GOTO 5970  'Negative
 4180 GOSUB 3000
 4190 LOCATE 23,1 : PRINT SPACE$(79);
 4200 REM Terminate record update if rec.no is negative
 4210 IF T1 < 0 THEN 5970
 4220 LOCATE 23,1
 4230 PRINT "Enter the Person's Surname (all capital letters): ";
 4240 LINE INPUT REPLY$
 4250 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4260 IF REPLY$ = "/" THEN 5970
 4270 IF REPLY$ = "" THEN 4300
 4280 T2$ = LEFT$(REPLY$,20)
 4290 GOSUB 3000
 4300 LOCATE 23,1 : PRINT SPACE$(79);
 4310 LOCATE 23,1
 4320 PRINT "Enter the Person's Given Names: ";
 4330 LINE INPUT REPLY$
 4340 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4350 IF REPLY$ = "/" THEN 5970
 4360 IF REPLY$ = "" THEN 4390
 4370 T3$ = LEFT$(REPLY$,30)
 4380 GOSUB 3000
 4390 LOCATE 23,1 : PRINT SPACE$(79); : LOCATE 23,1
 4400 PRINT "Enter the Person's Sex (";MALE.LTR$;" for ";MALE.SEX$;
 4410 PRINT " or ";FEMALE.LTR$;" for ";FEMALE.SEX$;"): ";
 4420 LINE INPUT REPLY$
 4430 LOCATE 24,1 : PRINT SPACE$(79);
 4440 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4450 IF REPLY$ = "/" THEN 5970
 4460 IF REPLY$ = "" THEN 4530
 4470 T4$ = LEFT$(REPLY$,2)
 4480 IF LEFT$(REPLY$,1) = CHR$(ASC(MALE.LTR$)+32) THEN T4$ = MALE.SEX$
 4490 IF LEFT$(REPLY$,1) = MALE.LTR$ THEN T4$ = MALE.SEX$
 4500 IF LEFT$(REPLY$,1) = CHR$(ASC(FEMALE.LTR$)+32) THEN T4$ = FEMALE.SEX$
 4510 IF LEFT$(REPLY$,1) = FEMALE.LTR$ THEN T4$ = FEMALE.SEX$
 4520 GOSUB 3090
 4530 LOCATE 23,1 : PRINT SPACE$(79);
 4540 IF CODE$ = "no" THEN 4630
 4550 LOCATE 23,1
 4560 LINE INPUT "Enter the Person's Code: ";REPLY$
 4570 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4580 IF REPLY$ = "/" THEN 5970
 4590 IF REPLY$ = "" THEN 4620
 4600 T5! = VAL(REPLY$)
 4610 GOSUB 3120
 4620 LOCATE 23,1 : PRINT SPACE$(79);
 4630 LOCATE 23,1
 4640 LINE INPUT "Enter the Father's Record Number: ";REPLY$
 4650 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4660 IF REPLY$ = "/" THEN 5970
 4670 IF REPLY$ = "" THEN 4740
 4680 T6 = VAL(REPLY$)
 4690 IF T6 >= 0 AND T6 <= MAX.PER THEN 4720
 4700 T6 = 0
 4710 LOCATE 22,1 : PRINT "Number out of Range"; : GOTO 4620
 4720 LOCATE 22,1 : PRINT SPACE$(79);
 4730 GOSUB 3140
 4740 LOCATE 23,1 : PRINT SPACE$(79);
 4750 LOCATE 23,1
 4760 LINE INPUT "Enter the Mother's Record Number: ";REPLY$
 4770 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4780 IF REPLY$ = "/" THEN 5970
 4790 IF REPLY$ = "" THEN 4860
 4800 T7 = VAL(REPLY$)
 4810 IF T7 >= 0 AND T7 <= MAX.PER THEN 4840
 4820 T7 = 0
 4830 LOCATE 22,1 : PRINT "Number out of Range"; : GOTO 4740
 4840 LOCATE 22,1 : PRINT SPACE$(79);
 4850 GOSUB 3320
 4860 LOCATE 23,1 : PRINT SPACE$(79);
 4870 LOCATE 23,1
 4880 PRINT "Enter the Person's Birth-date as: dd Mmm yyyy: ";
 4890 LINE INPUT REPLY$
 4900 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 4910 IF REPLY$ = "/" THEN 5970
 4920 IF REPLY$ = "" THEN 4960
 4930 REPLY$ = LEFT$(REPLY$,11)
 4940 RSET T8$ = REPLY$
 4950 GOSUB 3500
 4960 LOCATE 23,1 : PRINT SPACE$(79);
 4970 LOCATE 23,1
 4980 PRINT "Enter the Person's Birth-city: ";
 4990 LINE INPUT REPLY$
 5000 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5010 IF REPLY$ = "/" THEN 5970
 5020 IF REPLY$ = "" THEN 5050
 5030 T9$ = LEFT$(REPLY$,18)
 5040 GOSUB 3520
 5050 LOCATE 23,1 : PRINT SPACE$(79);
 5060 LOCATE 23,1
 5070 PRINT "Enter the Person's Birth-county: ";
 5080 LINE INPUT REPLY$
 5090 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5100 IF REPLY$ = "/" THEN 5970
 5110 IF REPLY$ = "" THEN 5140
 5120 T10$ = LEFT$(REPLY$,16)
 5130 GOSUB 3540
 5140 LOCATE 23,1 : PRINT SPACE$(79);
 5150 LOCATE 23,1
 5160 PRINT "Enter the Person's Birth-state or Country: ";
 5170 LINE INPUT REPLY$
 5180 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5190 IF REPLY$ = "/" THEN 5970
 5200 IF REPLY$ = "" THEN 5230
 5210 T11$ = LEFT$(REPLY$,16)
 5220 GOSUB 3560
 5230 LOCATE 23,1 : PRINT SPACE$(79);
 5240 LOCATE 23,1
 5250 PRINT "Enter the Person's Death-date as: dd Mmm yyyy: ";
 5260 LINE INPUT REPLY$
 5270 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5280 IF REPLY$ = "/" THEN 5970
 5290 IF REPLY$ = "" THEN 5330
 5300 REPLY$ = LEFT$(REPLY$,11)
 5310 RSET T12$ = REPLY$
 5320 GOSUB 3580
 5330 LOCATE 23,1 : PRINT SPACE$(79);
 5340 LOCATE 23,1
 5350 PRINT "Enter the Person's Death-city: ";
 5360 LINE INPUT REPLY$
 5370 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5380 IF REPLY$ = "/" THEN 5970
 5390 IF REPLY$ = "" THEN 5420
 5400 T13$ = LEFT$(REPLY$,18)
 5410 GOSUB 3600
 5420 LOCATE 23,1 : PRINT SPACE$(79);
 5430 LOCATE 23,1
 5440 PRINT "Enter the Person's Death-county: ";
 5450 LINE INPUT REPLY$
 5460 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5470 IF REPLY$ = "/" THEN 5970
 5480 IF REPLY$ = "" THEN 5510
 5490 T14$ = LEFT$(REPLY$,16)
 5500 GOSUB 3620
 5510 LOCATE 23,1 : PRINT SPACE$(79);
 5520 LOCATE 23,1
 5530 PRINT "Enter the Person's Death-state or Country: ";
 5540 LINE INPUT REPLY$
 5550 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5560 IF REPLY$ = "/" THEN 5970
 5570 IF REPLY$ = "" THEN 5600
 5580 T15$ = LEFT$(REPLY$,16)
 5590 GOSUB 3640
 5600 LOCATE 23,1 : PRINT SPACE$(79);
 5610 LOCATE 23,1
 5620 PRINT "Enter the Person's Burial-date as: dd Mmm yyyy: ";
 5630 LINE INPUT REPLY$
 5640 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5650 IF REPLY$ = "/" THEN 5970
 5660 IF REPLY$ = "" THEN 5700
 5670 REPLY$ = LEFT$(REPLY$,11)
 5680 RSET T16$ = REPLY$
 5690 GOSUB 3660
 5700 LOCATE 23,1 : PRINT SPACE$(79);
 5710 LOCATE 23,1
 5720 PRINT "Enter the Person's Burial-city: ";
 5730 LINE INPUT REPLY$
 5740 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5750 IF REPLY$ = "/" THEN 5970
 5760 IF REPLY$ = "" THEN 5790
 5770 T17$ = LEFT$(REPLY$,18)
 5780 GOSUB 3680
 5790 LOCATE 23,1 : PRINT SPACE$(79);
 5800 LOCATE 23,1
 5810 PRINT "Enter the Person's Burial-county: ";
 5820 LINE INPUT REPLY$
 5830 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5840 IF REPLY$ = "/" THEN 5970
 5850 IF REPLY$ = "" THEN 5880
 5860 T18$ = LEFT$(REPLY$,16)
 5870 GOSUB 3700
 5880 LOCATE 23,1 : PRINT SPACE$(79);
 5890 LOCATE 23,1
 5900 PRINT "Enter the Person's Burial-state or Country: ";
 5910 LINE INPUT REPLY$
 5920 IF REPLY$ = CHR$(34)+" "+CHR$(34) THEN REPLY$ = " "
 5930 IF REPLY$ = "/" THEN 5970
 5940 IF REPLY$ = "" THEN 5970
 5950 T19$ = LEFT$(REPLY$,16)
 5960 GOSUB 3720
 5970 REM Input is complete, now see if user wants to save the data
 5980 LOCATE 24,1 : PRINT SPACE$(79);
 5990 LOCATE 23,1 : PRINT SPACE$(79);
 6000 LOCATE 23,1
 6010 LINE INPUT "Type s (save), m (more), or f (forget): ";REPLY$
 6020 IF LEFT$(REPLY$,1) = "m" THEN LOCATE 23,1 : PRINT SPACE$(79); : GOTO 3980
 6030 IF LEFT$(REPLY$,1) = "M" THEN LOCATE 23,1 : PRINT SPACE$(79); : GOTO 3980
 6040 IF LEFT$(REPLY$,1) = "f" THEN KEY ON : CLS : KEY OFF : GOTO 2030
 6050 IF LEFT$(REPLY$,1) = "F" THEN KEY ON : CLS : KEY OFF : GOTO 2030
 6060 IF LEFT$(REPLY$,1) = "s" THEN LOCATE 22,1 : PRINT SPACE$(79); : GOTO 6100
 6070 IF LEFT$(REPLY$,1) = "S" THEN LOCATE 22,1 : PRINT SPACE$(79); : GOTO 6100
 6080 LOCATE 22,1 : PRINT "Error in reply";
 6090 GOTO 5990
 6100 REM Routine to SAVE the newly updated record
 6110 T1! = T1 : LSET F1$  = MKS$(T1!)
 6120 LSET F2$  = T2$
 6130 LSET F3$  = T3$
 6140 LSET F4$  = LEFT$(T4$,1)
 6150 LSET F5$  = MKS$(T5!)
 6160 T6! = T6 : LSET F6$  = MKS$(T6!)
 6170 T7! = T7 : LSET F7$  = MKS$(T7!)
 6180 RSET F8$  = T8$
 6190 LSET F9$  = T9$
 6200 LSET F10$  = T10$
 6210 LSET F11$  = T11$
 6220 RSET F12$  = T12$
 6230 LSET F13$  = T13$
 6240 LSET F14$  = T14$
 6250 LSET F15$  = T15$
 6260 RSET F16$  = T16$
 6270 LSET F17$  = T17$
 6280 LSET F18$  = T18$
 6290 LSET F19$  = T19$
 6300 PUT #1, REC.NO
 6310 KEY ON : CLS : KEY OFF
 6320 GOTO 2030
 6330 CLOSE #1
 6340 KEY ON : CLS : KEY OFF : LOCATE 21,1
 6350 rem PRINT "End of Program"
 6360 RUN CC.MENU$
 6370 REM Blank out a negative record
 6380 T2$ = ""
 6390 T3$ = ""
 6400 T4$ = ""
 6410 T5!= 0
 6420 T6 = 0
 6430 T7 = 0
 6440 T8$ = SPACE$(11)
 6450 T9$ = ""
 6460 T10$ = ""
 6470 T11$ = ""
 6480 T12$ = SPACE$(11)
 6490 T13$ = ""
 6500 T14$ = ""
 6510 T15$ = ""
 6520 T16$ = SPACE$(11)
 6530 T17$ = ""
 6540 T18$ = ""
 6550 T19$ = ""
 6560 RETURN
 